// ====================================
// == Statement
// ====================================

grammar Six
{
    rules
    {
        statement
            : statement-label? famous-statement ';'?
            | compiler-control-statement

        @lift
        famous-statement 
            : break-statement
            | continue-statement
            | fallthrough-statement
            | return-statement
            | throw-statement
            | for-in-statement
            | while-statement
            | repeat-while-statement
            | if-statement
            | guard-statement
            | switch-statement
            | do-statement
            | defer-statement
            | declaration
            | !in-switch expression // follow in switch/case
            | '{' #error("statement")

        code-block
            : '{' statement* '}'
            | '{' #error("code-block")


        break-statement
            : 'break' !in-switch label-name?
            | 'break' !in-switch #error("break-statement")

        continue-statement
            : 'continue' !in-switch label-name?
            | 'continue' !in-switch #error("continue-statement")

        fallthrough-statement
            = 'fallthrough'

        return-statement
            : 'return' !in-switch expression?
            | 'return' !in-switch #error("return-statement")

        throw-statement
            : 'throw' expression
            | 'throw' #error("throw-statement")

        for-in-statement:
            'for' 'case'? pattern 'in' expression where-clause? code-block

        while-statement
            : 'while' condition-list code-block
            | 'while' condition-list #error("while-statement - code-block")
            | 'while' #error("while-statement - condition-list")

        repeat-while-statement:
            'repeat' code-block 'while' expression

        if-statement
            : 'if' condition-list code-block else-clause?
            | 'if' condition-list #error("if-statement - code-block")
            | 'if' #error("if-statement - condition-list")

        else-clause
            : 'else' code-block
            | 'else' if-statement
            | &'else' #error("else-clause")

        guard-statement:
            'guard' condition-list 'else' code-block

        // ------------------------------------
        // 'do' statement
        // ------------------------------------

        do-statement
	        = 'do' code-block catch-clause*

        catch-clause
	        = 'catch' catch-pattern-list? code-block

        catch-pattern-list
	        = catch-pattern (',' catch-pattern)*

        catch-pattern
	        = pattern where-clause?

        // ------------------------------------
        // 'defer' statement
        // ------------------------------------

        defer-statement
	        = 'defer' code-block
    
        // ------------------------------------
        // statement-label
        // ------------------------------------

        statement-label
	        = label-name ':'

        label-name
	        = name

        // ------------------------------------
        // Misc
        // ------------------------------------
        in-switch:
            ('case'/'default') !more

        where-clause
            = 'where' where-expression

        where-expression
            = expression
    }
}
