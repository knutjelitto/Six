@namespace SixPeg
@classname SixParser
@using System.Globalization
@using System.Linq
@using System.Text.RegularExpressions
@using Six.Peg.Expression
   
axiom <GrammarPart>
    = grammar:grammar EOF                       { grammar }

grammar <GrammarPart>
    = extended:extendedGrammar                  { extended }
    / rules:simpleGrammar                       { new GrammarPart(rules) }

simpleGrammar  <Rules>
    = rules:rule*                               { new Rules(rules) }

extendedGrammar <GrammarPart>
    = _ "grammar" name:grammarName lbrace parts:grammarPart* rbrace
                                                { new GrammarPart(name, parts) }

grammarName
    = identifier

grammarPart <IGrules>
    = options
    / rules
    / terminals

options <Options>
    = _ "options" lbrace options:option* rbrace { new Options(options) }

rules <Rules>
    = _ "rules" lbrace rules:rule* rbrace       { new Rules(rules) }

terminals <Terminals>
    = _ "terminals" lbrace terminals:terminal* rbrace   { new Terminals(terminals) }

option <OptionExpression>
    = name:identifier equals value:identifier   { new OptionExpression(name, value)}

rulePreamble
    = attributes ruleName (equals/colon)

rule <RuleExpression>
    = attrs:attributes name:ruleName (equals/colon) expression:expression semicolon?
                                                { new RuleExpression(name, attrs, expression) }

ruleName
    = identifier
    / spacedName

terminal <TerminalExpression>
    = attrs:attributes name:ruleName (equals/colon) expression:expression semicolon?
                                                { new TerminalExpression(name, attrs, expression) }

attributes <Attributes>
    = attrs:attribute*                          { new Attributes(attrs) }

attribute <Symbol>
    = at id:identifier                          { id }

expression <AnyExpression>
    = choice

choice <AnyExpression>
    = choices:sequence<1,,(slash/bar)>          { new ChoiceExpression(choices) }

sequence <AnyExpression>
    = elements:element<0,,_>                    { new SequenceExpression(elements) }

element <AnyExpression> -memoize
    = prefixed
    / error

prefixed <AnyExpression>
    = and e:suffixed                            { new AndExpression(e) }
    / not e:suffixed                            { new NotExpression(e) }
    / before e:suffixed                         { new BeforeExpression(e) }
    / suffixed

suffixed <AnyExpression>
    = expression:primary q:quantifier           { new QuantifiedExpression(expression, q) }
    / expression:primary                        { expression }

quantifier <Quantifier> -memoize
    = question                                  { new Quantifier(min: 0, max: 1) }
    / star                                      { new Quantifier(min: 0, max: null) }
    / plus                                      { new Quantifier(min: 1, max: null) }

primary <AnyExpression> -memoize
    = !rulePreamble name:identifier             { new ReferenceExpression(name) }
    / !rulePreamble spaced:spacedName           { new SpacedExpression(spaced) }
    / !rulePreamble cls:class                   { cls }
    / stringLiteral
    / dot                                       { new WildcardExpression() }
    / lparen expression:expression rparen       { expression }

any_literal <object>
    = stringLiteral
    / integer

integer <object> -memoize -lexical
    = _ digits:(digit+ "")                      { new Integer(digits) }

error <AnyExpression>
    = hash _ "error" lparen arguments:any_literal<1,,comma> rparen { new ErrorExpression(arguments) }
    / hash _ "error" lparen #error{ "error-error-error" }
    / hash #error{ "error-error" }

at        -lexical = _ "@"
hash      -lexical = _ "#"
equals    -lexical = _ "="
semicolon -lexical = _ ";"
colon     -lexical = _ ":"
slash     -lexical = _ "/"
bar       -lexical = _ "|"
and       -lexical = _ "&"
not       -lexical = _ "!"
before    -lexical = _ "<"
question  -lexical = _ "?"
star      -lexical = _ "*"
plus      -lexical = _ "+"
lparen    -lexical = _ "("
rparen    -lexical = _ ")"
lbrace    -lexical = _ "{"
rbrace    -lexical = _ "}"
dot       -lexical = _ "."
comma     -lexical = _ ","

lbracket  -lexical = "["
rbracket  -lexical = "]"

identifier <Symbol> -memoize -lexical
    = _ name:(identifier_head identifier_tail*) { new Symbol(Source, name, nameStart.Location, nameEnd.Location, false) }
    / _ name:'_'  { new Symbol(Source, name, nameStart.Location, nameEnd.Location, false) }

identifier_head
    = _ start:(letter (letter / digit)*)        { start }

identifier_tail
    = tail:(('-'/'_') (letter / digit)+)        { tail }

spacedName <Symbol> -memoize -lexical
    = _ start:"'" chars:singleQuotedCharacter* end:"'"
                                                { new Symbol(Source, string.Join("", chars), startStart.Location, endEnd.Location, true) }
    / _ start:"'" chars:singleQuotedCharacter* #error{ "PEG0009:" + Resources.PEG0009_ERROR_PARSER_UnterminatedString }
                                                { new Symbol(Source, string.Join("", chars), startStart.Location, 0, true) }

singleQuotedCharacter
  = !("'" / eolChar) .

stringLiteral <AnyExpression> -lexical
  = value:string                                { new CharacterSequenceExpression(value) }

string -lexical
  = _ string:doubleQuotedString                 { @string }

doubleQuotedString
  = '"' chars:doubleQuotedCharacter* ('"' / #error{ "PEG0009:" + Resources.PEG0009_ERROR_PARSER_UnterminatedString })
                                                { string.Concat(chars) }

doubleQuotedCharacter
  = simpleDoubleQuotedCharacter
  / simpleEscapeSequence
  / zeroEscapeSequence
  / hexEscapeSequence
  / unicodeEscapeSequence
  / eolEscapeSequence

simpleDoubleQuotedCharacter
  = !('"' / "\\" / eolChar) char:. { @char }

class <AnyExpression> -lexical
    = _ lbracket
        inverted:"^"? parts:(classCharacterRange / classCharacter)*
        (rbracket / #error{ "PEG0010:" + Resources.PEG0010_ERROR_PARSER_UnterminatedClass })
        {
            new CharacterClassExpression(parts, negated: inverted.SingleOrDefault() == "^")
        }

classCharacterRange <CharacterRangeExpression>
  = begin:bracketDelimitedCharacter "-" end:bracketDelimitedCharacter {
        new CharacterRangeExpression(begin[0], end[0])
    }

classCharacter <CharacterRangeExpression>
  = char_:bracketDelimitedCharacter {
        new CharacterRangeExpression(char_[0], char_[0])
    }

bracketDelimitedCharacter -memoize
  = simpleBracketDelimitedCharacter
  / simpleEscapeSequence
  / zeroEscapeSequence
  / hexEscapeSequence
  / unicodeEscapeSequence
  / eolEscapeSequence

simpleBracketDelimitedCharacter
  = !("]" / "\\" / eolChar) .

simpleEscapeSequence
  = "\\" !(digit / "x" / "u" / eolChar) character:. {
        character
            .Replace("b", "\b")
            .Replace("f", "\f")
            .Replace("n", "\n")
            .Replace("r", "\r")
            .Replace("t", "\t")
            .Replace("v", "\v")
    }

zeroEscapeSequence
  = "\\0" !digit { "\0" }

hexEscapeSequence
  = "\\x" digits:(hexDigit hexDigit) {
        ((char)Convert.ToInt32(digits, 16)).ToString()
    }

unicodeEscapeSequence
  = "\\u" digits:(hexDigit hexDigit hexDigit hexDigit) {
        ((char)Convert.ToInt32(digits, 16)).ToString()
    }

eolEscapeSequence
  = "\\" eol:eol { eol }

digit
  = [0-9]

hexDigit
  = [0-9a-fA-F]

letter
  = lowerCaseLetter
  / upperCaseLetter

lowerCaseLetter
  = [a-z]

upperCaseLetter
  = [A-Z]

_ -memoize = (whitespace / eol / comment)*

comment -memoize
  = singleLineComment
  / multiLineComment

singleLineComment -memoize -lexical
  = "//" (!eolChar .)*

multiLineComment -lexical
  = "/*" (!"*/" .)* "*/"

eol -memoize
  = "\n"
  / "\r\n"
  / "\r"
  / "\u2028"
  / "\u2029"

eolChar
  = [\n\r\u2028\u2029]

whitespace -memoize -lexical
  = [ \t\v\f\u00A0\uFEFF\u1680\u180E\u2000-\u200A\u202F\u205F\u3000]

EOF
  = _ !.
  / unexpected:&. #error{ "PEG0008:" + string.Format(CultureInfo.CurrentCulture, Resources.PEG0008_ERROR_PARSER_UnexpectedEndOfInput, unexpected) }
