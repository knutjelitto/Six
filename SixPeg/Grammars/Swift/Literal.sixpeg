// ====================================
// == Identifier / Literal
// ====================================

name
    = _ identifier

identifier
    = identifier-head identifier-character*
    / [`] identifier-head identifier-character* [`]
    / implicit-parameter-name
    / property-wrapper-projection

identifier-head
    = [a-zA-Z_]

identifier-character
    = identifier-head
    / [0-9]

more
    = identifier-character

implicit-parameter-name
    = [$] decimal-digits

property-wrapper-projection
    = [$] identifier-character+


integer-literal
    = binary-literal
    / octal-literal
    / hexadecimal-literal
    / decimal-literal

binary-literal
    = [0][b] binary-digit binary-literal-character*

binary-digit
    = [01]

binary-literal-character
    = binary-digit
    / [_]

octal-literal
    = [0][o] octal-digit octal-literal-character*

octal-digit
    = [0-7]

octal-literal-character
    = octal-digit
    / [_]

decimal-literal
    = decimal-digit decimal-literal-character*

nonzero-decimal-literal
    = [0]* decimal-digit-except-0 decimal-digit*

decimal-digit
    = [0-9]

decimal-digit-except-0
    = [1-9]

decimal-literal-character
    = decimal-digit
    / [_]

hexadecimal-literal
    = [0][x] hexadecimal-digit hexadecimal-literal-character*

hexadecimal-digit
    = [0-9a-fA-F]
hex-digit
    = hexadecimal-digit

hexadecimal-literal-character
    = hexadecimal-digit
    / [_]


floating-point-literal
    = hexadecimal-literal hexadecimal-fraction? hexadecimal-exponent?
    / decimal-literal decimal-fraction? decimal-exponent?

decimal-fraction
    = [.] decimal-literal

decimal-exponent
    = floating-point-e sign? decimal-literal

floating-point-e
    = [eE]

hexadecimal-fraction
    = [.] hexadecimal-digit hexadecimal-literal-character*

hexadecimal-exponent
    = floating-point-p sign? decimal-literal

floating-point-p
    = [pP]

sign
    = [+-]


//
// ad-hoc
//
decimal-digits
    = _ [0-9]+




